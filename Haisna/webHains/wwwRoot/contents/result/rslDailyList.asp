<%@ LANGUAGE="VBScript" %>
<%
'-----------------------------------------------------------------------------
'		結果入力（受診者一覧） (Ver0.0.1)
'		AUTHER  : Tatsuhiko Nishi@Takumatec.co.jp
'-----------------------------------------------------------------------------
Option Explicit
%>
<!-- #include virtual = "/webHains/includes/checkSession.inc"         -->
<!-- #include virtual = "/webHains/includes/common.inc"               -->
<!-- #include virtual = "/webHains/includes/editNumberList.inc"       -->
<!-- #include virtual = "/webHains/includes/EditPageNavi.inc"         -->
<!-- #include virtual = "/webHains/includes/EditRslDailyList.inc"     -->
<!-- #include virtual = "/webHains/includes/tokyu_editCourseList.inc" -->
<%
'セッション・権限チェック
'Call CheckSession(BUSINESSCD_NOTHING, CHECKSESSION_SELF)

'-----------------------------------------------------------------------------
' 共通宣言部
'-----------------------------------------------------------------------------
Const ACTMODE_SAVEEND  = "saveend"	'動作モード(保存完了)

'データベースアクセス用オブジェクト
Dim objCommon		'共通クラス
Dim objConsult		'受診情報アクセス用

'前画面から送信されるパラメータ値
Dim strActMode		'動作モード
Dim lngCslYear		'受診日(年)
Dim lngCslMonth		'受診日(月)
Dim lngCslDay		'受診日(日)
Dim strCsCd			'コース
Dim strSortKey		'表示順
Dim strCntlNo		'管理番号
Dim lngStartPos		'表示開始位置
Dim strGetCount		'取得件数

'受診情報一覧取得時に使用する変数
Dim dtmCslDate		'受診日
Dim lngCntlNo		'管理番号
Dim lngGetCount		'取得件数

'受診情報
Dim strArrRsvNo		'予約番号の配列
Dim strArrDayId		'当日IDの配列
Dim strArrWebColor	'webカラーの配列
Dim strArrCsName	'コース名の配列
Dim strArrPerId		'個人IDの配列
Dim strArrLastName	'姓の配列
Dim strArrFirstName	'名の配列
Dim lngCount		'レコード件数

Dim strArrSortKey		'表示順
Dim strArrSortKeyName	'表示順名称

Dim strPageMaxLine		'表示行数
Dim strPageMaxLineName	'表示行数名称

Dim lngCntlNoFlg	'管理番号制御フラグ
Dim strURL			'URL文字列
Dim i				'インデックス

'-----------------------------------------------------------------------------
' 先頭制御部
'-----------------------------------------------------------------------------
'オブジェクトのインスタンス作成
Set objConsult = Server.CreateObject("HainsConsult.Consult")
Set objCommon  = Server.CreateObject("HainsCommon.Common")

'引数値の取得
strActMode  = Request("actMode")
lngCslYear  = CLng("0" & Request("cslYear") )
lngCslMonth = CLng("0" & Request("cslMonth"))
lngCslDay   = CLng("0" & Request("cslDay")  )
strCsCd     = Request("csCd")
strSortKey  = Request("sortKey")
strCntlNo   = Request("cntlNo")
lngStartPos = CLng("0" & Request("startPos"))
strGetCount = Request("getCount")

'検索開始位置未指定時は先頭から検索する
lngStartPos = IIf(lngStartPos = 0, 1, lngStartPos)

'取得件数未指定時はデフォルト値を取得
strGetCount = IIf(strGetCount = "", objCommon.SelectRslPageMaxLine, strGetCount)

'管理番号の制御方法を取得
lngCntlNoFlg = CLng("0" & objCommon.SelectCntlFlg)
%>
<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.01 Transitional//EN">
<HTML LANG="ja">
<HEAD>
<META HTTP-EQUIV="content-type" CONTENT="text/html;charset=Shift_JIS">
<meta http-equiv="Content-Style-Type"  content="text/css">
<meta http-equiv="Content-Script-Type" content="text/javascript">
<LINK REL="stylesheet" TYPE="text/css" HREF="/webHains/contents/css/default.css">
<TITLE>結果入力（受診者一覧）</TITLE>
<!-- #include virtual = "/webHains/includes/calGuide.inc" -->
<SCRIPT TYPE="text/javascript">
<!-- #include virtual = "/webHains/includes/Date.inc"     -->
<!--
// 入力チェック
function checkData() {

	var myForm = document.entryForm;	// 自画面のフォームエレメント
	var ret    = false;					// 関数戻り値

	// 受診日入力チェック
	for ( ; ; ) {

		if ( !isDate( myForm.cslYear.value, myForm.cslMonth.value, myForm.cslDay.value ) ) {
			alert('受診日の形式に誤りがあります。');
			break;
		}

		// 管理番号入力チェック
		if ( myForm.cntlNo.value != '' ) {
			if ( !myForm.cntlNo.value.match('^[0-9]+$') ) {
				alert('管理番号には1〜9999の値を入力して下さい。');
				break;
			}
		}

		ret = true;
		break;
	}

	return ret;
}

// 結果詳細表示
function showDetail(rsvNo, dayId) {

	var code = '';	// 検査グループコード
	var url;		// URL文字列

	// 既に詳細が表示されている場合は、検査項目グループを取得
	if ( top.result.location.pathname == '/webHains/contents/result/rslDetail.asp' ) {
		if ( top.result.resultList != null ) {
			code = top.result.resultList.code.value;
		}
	}

	// URL文字列の編集
	url = 'rslDetail.asp';
	url = url + '?rsvNo='    + rsvNo;
	url = url + '&cslYear='  + '<%= lngCslYear  %>';
	url = url + '&cslMonth=' + '<%= lngCslMonth %>';
	url = url + '&cslDay='   + '<%= lngCslDay   %>';
	url = url + '&cntlNo='   + '<%= strCntlNo   %>';
	url = url + '&csCd='     + '<%= strCsCd     %>';
	url = url + '&sortKey='  + '<%= strSortKey  %>';
	url = url + '&dayId='    + dayId;
	url = url + '&code='     + code;

	// 結果詳細表示
	top.result.location.replace(url);
	return false;
}
//-->
</SCRIPT>
<style type="text/css">
	body { margin: 10px 0 0 5px; }
	td.rsltab { background-color:#FFFFFF }
</style>
</HEAD>
<%
'保存完了時以外は結果入力フレームを強制的に空白にする
If strActMode = ACTMODE_SAVEEND Then
%>
	<BODY>
<%
Else
%>
	<BODY ONLOAD="javascript:top.result.location.replace('/webHains/contents/common/Blank.htm')">
<%
End If
%>
<FORM NAME="entryForm" ACTION="<%= Request.ServerVariables("SCRIPT_NAME") %>" ONSUBMIT="javascript:return checkData()">
<!-- 表題 -->
<TABLE BORDER="0" CELLPADDING="2" CELLSPACING="1" BGCOLOR="#999999" WIDTH="255"><!-- or WIDTH="90%" -->
	<TR>
		<TD HEIGHT="15" BGCOLOR="#ffffff" NOWRAP><B><SPAN CLASS="result">■</SPAN><FONT COLOR="#000000">受診者一覧</FONT></B></TD>
	</TR>
</TABLE>

<BR>

	<TABLE BORDER="0" CELLPADDING="0" CELLSPACING="0">
		<TR>
			<TD NOWRAP>受診日</TD>
			<TD>：</TD>
			<TD>
				<TABLE BORDER="0" CELLPADDING="1" CELLSPACING="0">
					<TR>
						<TD><A HREF="javascript:calGuide_showGuideCalendar('cslYear', 'cslMonth', 'cslDay')"><IMG SRC="/webHains/images/question.gif" WIDTH="21" HEIGHT="21" ALT="日付ガイドを表示"></A></TD>
						<TD><%= EditNumberList("cslYear", YEARRANGE_MIN, YEARRANGE_MAX, lngCslYear, False) %></TD>
						<TD>年</TD>
						<TD><%= EditNumberList("cslMonth", 1, 12, lngCslMonth, False) %></TD>
						<TD>月</TD>
						<TD><%= EditNumberList("cslDay", 1, 31, lngCslDay, False) %></TD>
						<TD>日</TD>
					</TR>
				</TABLE>
			</TD>
		</TR>
		<TR>
			<TD HEIGHT="3"></TD>
		</TR>
		<TR>
			<TD NOWRAP>コース</TD>
			<TD>：</TD>
			<TD><%= Tokyu_EditCourseList(EDITCOURSELIST_MODE_MAIN, "csCd", strCsCd, SELECTED_ALL, False) %></TD>
		</TR>
		<TR>
			<TD HEIGHT="4"></TD>
		</TR>
		<TR>
			<TD NOWRAP>表示順</TD>
			<TD>：</TD>
			<TD><%= EditSortKeyList("sortKey", strSortKey) %></TD>
		</TR>
<%
		'管理番号を使用する場合は入力テキストを表示
		If lngCntlNoFlg = CNTLNO_ENABLED Then
%>
			<TR>
				<TD HEIGHT="4"></TD>
			</TR>
			<TR>
				<TD NOWRAP>管理番号</TD>
				<TD>：</TD>
				<TD><INPUT TYPE="text" NAME="cntlNo" SIZE="<%= TextLength(4) %>" MAXLENGTH="4" VALUE="<%= strCntlNo %>"></TD>
			</TR>
<%
		End If
%>
		<TR>
			<TD NOWRAP>件数</TD>
			<TD>：</TD>
			<TD>
<%
				If lngCntlNoFlg <> CNTLNO_ENABLED Then
%>
					<INPUT TYPE="hidden" NAME="cntlNo" VALUE="">
<%
				End If
%>
				<TABLE BORDER="0" CELLPADDING="1" CELLSPACING="0">
					<TR>
						<TD>
<%
							'表示行数情報の読み込み
							If objCommon.SelectRslPageMaxLineList(strPageMaxLine, strPageMaxLineName) > 0 Then
								Response.Write EditDropDownListFromArray("getCount", strPageMaxLine, strPageMaxLineName, strGetCount, NON_SELECTED_DEL)
							End If
%>
						</TD>
						<TD><INPUT TYPE="image" SRC="/webHains/images/b_prev.gif" WIDTH="53" HEIGHT="28" ALT="指定条件で表示"></TD>
					</TR>
				</TABLE>
			</TD>
		</TR>
	</TABLE>

	<BR>

	<SPAN style="font-size:9pt;">「<FONT color="#FF6600"><B><%= lngCslYear %>年<%= lngCslMonth %>月<%= lngCslDay %>日</B></FONT>」<BR>の来院済み受診者一覧を表示しています。<BR></SPAN>
<%
	'受診者一覧編集
	Do
		'受診日・管理番号の設定
		dtmCslDate = CDate(lngCslYear & "/" & lngCslMonth & "/" & lngCslDay)
		lngCntlNo  = CLng("0" & strCntlNo)

		'取得件数の設定
		lngGetCount = 0
		If IsNumeric(strGetCount) Then
			lngGetCount = CLng(strGetCount)
		End If

		'検索条件を満たす受診情報を取得
'## 2004.01.09 Mod By T.Takagi@FSIT 来院関連追加
'		lngCount = objConsult.SelectConsultList(dtmCslDate,     _
'												lngCntlNo,      _
'												strCsCd, , , ,  _
'												lngStartPos,    _
'												lngGetCount,    _
'												strSortKey,     _
'												True, , , ,     _
'												strArrRsvNo,    _
'												strArrDayId,    _
'												strArrWebColor, _
'												strArrCsName,   _
'												strArrPerId,    _
'												strArrLastName, _
'												strArrFirstName)
		'来院者のみ取得
		lngCount = objConsult.SelectConsultList(dtmCslDate,     _
												lngCntlNo,      _
												strCsCd, , , ,  _
												lngStartPos,    _
												lngGetCount,    _
												strSortKey,     _
												True, , , ,     _
												strArrRsvNo,    _
												strArrDayId,    _
												strArrWebColor, _
												strArrCsName,   _
												strArrPerId,    _
												strArrLastName, _
												strArrFirstName, , , , , , , , , , , , , , , , , _
												True)
'## 2004.01.09 Mod End
%>
		対象者数は <FONT COLOR="#ff6600"><B><%= lngCount %></B></FONT>人です。<BR><BR>
<%
		'対象データが存在しない場合は編集を抜ける
		If IsEmpty(strArrRsvNo) Then
			Exit Do
		End If
%>
		<TABLE BORDER="0" CELLSPACING="2" CELLPADDING="1">
			<TR BGCOLOR="#cccccc">
				<TD NOWRAP>当日ＩＤ</TD>
				<TD NOWRAP>受診コース</TD>
				<TD NOWRAP>個人氏名</TD>
				<TD NOWRAP>個人ＩＤ</TD>
			</TR>
<%
			For i = 0 To UBound(strArrRsvNo)
%>
				<TR BGCOLOR="#ffffff">
					<TD><%= IIf(strArrDayId(i) <> "", objCommon.FormatString(strArrDayId(i), "0000"), "&nbsp;") %></TD>
					<TD NOWRAP><FONT COLOR="<%= strArrWebColor(i) %>">■</FONT><%= strArrCsName(i) %></TD>
					<TD NOWRAP><A HREF="javascript:function voi(){};voi()" ONCLICK="javascript:showDetail('<%= strArrRsvNo(i) %>', '<%= strArrDayId(i) %>')"><%= strArrLastName(i) & "　" & strArrFirstName(i) %></A></TD>
					<TD><%= strArrPerId(i) %></TD>
				</TR>
<%
			Next
%>
		</TABLE>
<%
		'取得件数未指定時はページングナビゲータ不用
		If lngGetCount = 0 Then
			Exit Do
		End If

		'URL文字列の編集
		strURL = Request.ServerVariables("SCRIPT_NAME")
		strURL = strURL & "?cslYear="  & lngCslYear
		strURL = strURL & "&cslMonth=" & lngCslMonth
		strURL = strURL & "&cslDay="   & lngCslDay
		strURL = strURL & "&csCd="     & strCsCd
		strURL = strURL & "&sortKey="  & strSortKey
		strURL = strURL & "&cntlNo="   & strCntlNo

		'ページングナビゲータの編集
		Response.Write EditPageNavi(strURL, lngCount, lngStartPos, lngGetCount)

		Exit Do
	Loop
%>
</FORM>
</BODY>
</HTML>